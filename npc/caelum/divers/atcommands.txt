//------------------------------------------------------------//
//----- Caelum Custom Script ---------------------------------//
//
//  @commands
//
// - Créé par : Sky
//
// - Description : @commands custom de Caelum
//
//------------------------------------------------------------//

// -- @reloadnpc -----------------------------------------------
// Reload un seul fichier de npc
-	script	atcommand_reloadnpc	-1,{
	end;

OnInit:
	bindatcmd "reloadnpc",strnpcinfo(0)+"::OnAtcommand",4,100,1;
	end;
	
OnAtcommand:
	if (.@atcmd_numparameters == 1) {
		atcommand "@unloadnpcfile " + .@atcmd_parameters$[0];
		sleep2 1000;
		atcommand "@loadnpc " + .@atcmd_parameters$[0];
	}
	else {
		dispbottom "Chemin du script invalide, doit être sous la forme npc/caelum/script.txt";
	}
	end;
}

// -- @storage -----------------------------------------------
// Bloque l'utilisation de @storage sur les maps GvG
-	script	atcommand_storage	-1,{
	end;

OnInit:
	bindatcmd "storage",strnpcinfo(0)+"::OnAtcommand";
	end;
	
OnAtcommand:
	if(getmapflag(strcharinfo(3),mf_gvg_castle)) {
		dispbottom "Impossible d'ouvrir le Storage dans un chateau";
		end;
	}
	openstorage;
	end;
}

// -- @gstorage -----------------------------------------------
// Bloque l'utilisation de @gstorage sur les maps GvG
-	script	atcommand_gstorage	-1,{
	end;

OnInit:
	bindatcmd "gstorage",strnpcinfo(0)+"::OnAtcommand";
	end;
	
OnAtcommand:
	if(getmapflag(strcharinfo(3),mf_gvg_castle)) {
		dispbottom "Impossible d'ouvrir le Guild Storage dans un chateau";
		end;
	}
	guildopenstorage;
	end;
}

